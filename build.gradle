plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile group: 'nl.esciencecenter.xenon', name: 'xenon', version: '1.1.0-beta1'
}

shadowJar {
  baseName = 'Xenon-examples'
}

// create a single Jar with only local dependencies
// all dependencies need to be put in the lib directory
// first
configurations {
    compileLocal
}
dependencies {
    compileLocal fileTree(dir: file('lib'), include: '*.jar')
}
task downloadDependencies(type: Copy) {
    from configurations.runtime.files
    into file('lib')
}
task localJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Xenon-examples-testing'
    }
    baseName = project.name + '-testing'
    from(configurations.compileLocal.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

defaultTasks 'shadowJar'
